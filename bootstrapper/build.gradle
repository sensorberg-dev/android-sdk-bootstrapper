
repositories {
    mavenCentral()
    maven {
        url "https://raw.github.com/sensorberg-dev/android-sdk/mvn-repo";
    }
    maven {
        url "http://172.17.12.103:8881/nexus/content/repositories/snapshots"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'android-maven'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 19
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    compile 'com.sensorberg.sdk:sensorberg-sdk:0.8.0-SNAPSHOT@aar'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

group = "com.sensorberg.sdk.bootstrapper"
artifactId = "sensorberg-sdk-bootstrapper"
version = VERSION

project.ext.tagName = "v${VERSION}-bootstrapper"
project.ext.branch = "mvn-repo"
//the name of our origin
project.ext.currentRemoteName = "origin"
project.ext.releaseRepo = "git@github.com:sensorberg-dev/android-sdk.git"



if (System.getProperty("sensorberg.useGithubRepo")) {
    project.ext.repoDir = "${buildDir.path}/github-android-sdk"
    version = "${VERSION}"

    uploadArchives.mustRunAfter(prepareGithubUpload)
    pushToGithub.mustRunAfter(uploadArchives)

    uploadArchives.dependsOn(prepareGithubUpload)
    pushToGithub.dependsOn(uploadArchives)

    prepareGithubUpload.dependsOn(verifyNoSnapshotDependencies)
    prepareGithubUpload.dependsOn(verifyReleaseBranch)
    prepareGithubUpload.dependsOn(verifyUpload)

} else {
    project.ext.repoDir = "file://${System.getProperty("user.home")}/.m2/repository/"
    version = "${VERSION}-SNAPSHOT"
}

apply from: "verifyTasks.gradle"
apply from: "uploadGithub.gradle"

uploadArchives {
    repositories {
        mavenDeployer {

            repository(url: "file://${project.ext.repoDir}")

            snapshotRepository(url: "http://172.17.12.103:8881/nexus/content/repositories/snapshots"){
                authentication(userName: System.getProperty("sensorberg.nexusUsername"), password: System.getProperty("sensorberg.nexusPassword"))
            }

            pom.groupId = "com.sensorberg.sdk.bootstrapper"

            pom.project {
                inceptionYear '2014'
                name 'Sensorberg Android SDK Bootstrapper'
                artifactId "sensorberg-sdk-bootstrapper"
                packaging 'aar'
                description 'Android SDK bootstrapper that helps to integrate the SDK'
                url 'https://github.com/sensorberg-dev/android-sdk'

                scm {
                    url 'https://github.com/sensorberg-dev/android-sdk-bootstrapper'
                    connection 'https://github.com/sensorberg-dev/android-sdk-bootstrapper'
                    developerConnection 'https://github.com/sensorberg-dev/android-sdk-bootstrapper'
                }
            }
        }

    }
}
